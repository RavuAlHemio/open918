import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'net.nemerosa.versioning' version '2.6.1'
}

group 'org.open918'
version '1.1-'+versioning.info.commit.substring(0, 8)

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.1'
    runtimeOnly 'com.sun.xml.bind:jaxb-impl:4.0.4'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.54'
    testImplementation group: 'junit', name: 'junit', version: '4.11'
}

jar {
    manifest {
        attributes(
                'Built-By'       : System.properties['user.name'],
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Revision' : versioning.info.commit,
                'Created-By'     : "Gradle ${gradle.gradleVersion}",
                'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}